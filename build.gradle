plugins {
    id 'java'
    id 'org.springframework.boot' version '2.4.3'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
}

group = 'com.doroshenko.serhey'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

bootJar {
    manifest {
        attributes("Implementation-Title": project.name, "Implementation-Version": project.version)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven {
        url "https://maven.pkg.github.com/spring-cloud-portfolio/person-api"
        credentials {
            username = findProperty('gitHubUsername') ?: System.getenv('GIT_HUB_USR')
            password = findProperty('gitHubPassword') ?: System.getenv('GIT_HUB_PSW')
        }
    }
}

ext {
    set('springCloudVersion', "2020.0.1")
    set('testcontainersVersion', "1.15.1")
    set('mapStructVersion', '1.4.2.Final')
    set("dockerImageProfile", project.findProperty("dip") ?: 'default')
}

dependencies {
    /* Custom */
    implementation 'com.doroshenko.serhey:person-api:+'
    /*MapStruct*/
    implementation("org.mapstruct:mapstruct:${mapStructVersion}")
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"
    /* Web */
    implementation 'org.springframework.boot:spring-boot-starter-web'
    /* Cloud */
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    /* Security */
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    /* Persistence */
    runtimeOnly 'org.postgresql:postgresql'
    implementation 'org.flywaydb:flyway-core'
    annotationProcessor 'org.hibernate:hibernate-jpamodelgen'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    /* Cache */
    implementation('com.github.ben-manes.caffeine:caffeine')
    /* Spring tools */
//    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    /* Test */
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}

bootBuildImage {
    publish = true
    cleanCache = false
    environment = ["BP_JVM_VERSION": "15.*"]
    imageName = "cloudportfolio/${project.name}-${dockerImageProfile}"
    docker {
        publishRegistry {
            username = findProperty('dockerHubUsername') ?: System.getenv('DOCKER_HUB_USR')
            password = findProperty('dockerHubPassword') ?: System.getenv('DOCKER_HUB_PSW')
        }
    }
}
